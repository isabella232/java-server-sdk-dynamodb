
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
}

plugins {
    id "java"
    id "java-library"
    id "checkstyle"
    id "signing"
    id "com.github.johnrengelman.shadow" version "4.0.4"
    id "maven-publish"
    id "de.marcphilipp.nexus-publish" version "0.3.0"
    id "io.codearte.nexus-staging" version "0.21.2"
    id "org.ajoberstar.git-publish" version "2.1.3"
    id "idea"
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

configurations.all {
    // check for updates every build for dependencies with: 'changing: true'
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    mavenLocal()
    // Before LaunchDarkly release artifacts get synced to Maven Central they are here along with snapshots:
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    mavenCentral()
}

allprojects {
    group = 'com.launchdarkly'
    version = "${version}"
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

ext {
    sdkBasePackage = "com.launchdarkly.client.dynamodb"
    sdkBaseName = "launchdarkly-java-server-sdk-dynamodb-store"
}

ext.libraries = [:]

// Add dependencies to "libraries.internal" that are not exposed in our public API. These
// will be embedded with shaded names in the uberjar.
libraries.internal = [
    "com.google.guava:guava:19.0"
]

// Add dependencies to "libraries.external" that are exposed in our public API, or that have
// global state that must be shared between the SDK and the caller. These are not embedded
// in the uberjar; the caller must provide them on the classpath at runtime.
libraries.external = [
    "com.launchdarkly:launchdarkly-java-server-sdk:4.12.0",
    "software.amazon.awssdk:dynamodb:2.10.32",
    "org.slf4j:slf4j-api:1.7.21"
]

// Add dependencies to "libraries.test" that are used only in unit tests.
libraries.test = [
    "org.hamcrest:hamcrest-all:1.3",
    "junit:junit:4.12",
    "ch.qos.logback:logback-classic:1.1.7",
    "com.launchdarkly:launchdarkly-java-server-sdk:4.12.0:test" // our unit tests use helper classes from the SDK
]

dependencies {
    implementation libraries.internal
    compileClasspath libraries.external
    runtime libraries.internal
    testImplementation libraries.test, libraries.internal, libraries.external

    // Unlike what the name might suggest, the "shadow" configuration specifies dependencies that
    // should *not* be shaded by the Shadow plugin when we build our shaded jars.
    shadow libraries.external
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

shadowJar {
    baseName = sdkBaseName
    
    // No classifier means that the shaded jar becomes the default artifact
    classifier = ''

    manifest {
        attributes("Implementation-Version": version)
    }
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "com.launchdarkly.shaded"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

artifacts {
    archives sourcesJar, javadocJar, shadowJar
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

nexusStaging {
    packageGroup = "com.launchdarkly"
    numberOfRetries = 40 // we've seen extremely long delays in closing repositories
}

def pomConfig = {
    name 'LaunchDarkly Java SDK DynamoDB integration'
    packaging 'jar'
    url 'https://github.com/launchdarkly/java-server-sdk-dynamodb'

    licenses {
        license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            id 'jkodumal'
            name 'John Kodumal'
            email 'john@launchdarkly.com'
        }
    }

    scm {
        connection 'scm:git:git://github.com/launchdarkly/java-server-sdk-dynamodb.git'
        developerConnection 'scm:git:ssh:git@github.com:launchdarkly/java-server-sdk-dynamodb.git'
        url 'https://github.com/launchdarkly/java-server-sdk-dynamodb'
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)

            artifactId = sdkBaseName
            artifact sourcesJar
            artifact javadocJar
            
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'DynamoDB-backed data store for the LaunchDarkly Java SDK')

                // The following workaround is for a known issue where the Shadow plugin assumes that all
                // non-shaded dependencies should have "runtime" scope rather than "compile".
                //    https://github.com/johnrengelman/shadow/issues/321
                // Currently there doesn't seem to be any way way around this other than simply hacking the
                // pom at this point after it has been generated by Shadow. All of the dependencies that
                // are in the pom at this point should have compile scope.
                def dependenciesNode = root.getAt('dependencies').get(0)
                dependenciesNode.each { dependencyNode ->
                    def scopeNode = dependencyNode.getAt('scope').get(0)
                    scopeNode.setValue('compile')
                }

                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

nexusPublishing {
    clientTimeout = java.time.Duration.ofMinutes(3) // we've seen extremely long delays in creating repositories
    repositories {
        sonatype {
            username = ossrhUsername
            password = ossrhPassword            
        }
    }
}

signing {
    sign publishing.publications.shadow
}

gitPublish {
    repoUri = 'git@github.com:launchdarkly/java-server-sdk-dynamodb.git'
    branch = 'gh-pages'
    contents {
        from javadoc
    }
    commitMessage = 'publishing javadocs'
}
